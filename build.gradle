buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "http://repo.springsource.org/milestone/"
        }
        jcenter()
    }

    dependencies {
        classpath "com.netflix.nebula:gradle-ospackage-plugin:2.2.6"
    }
}

allprojects {
    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                url "http://repo.springsource.org/milestone/"
            }
            jcenter()
        }
    }
    group "com.rsa.asoc.sa.ui"
    version "10.6.0.0-SNAPSHOT"

    apply plugin: "os-package-base"

    ext {
        rpmScriptsDir = "build/rpm/scripts"
    }

    task rpmScripts(type: Copy) {
        from(fileTree("rpm/scripts"))
        into project.rpmScriptsDir
        expand project.properties
    }

    afterEvaluate { Project project ->
        if (project.hasProperty("createRPM") && project.createRPM) {

            task rpm(type:Rpm, dependsOn: ["build", "rpmScripts"]) {

                packageName project.archivesBaseName
                packageDescription "RSA Security Analytics - RPM"
                version project.version.split("-")[0]
                release "el6"
                arch "noarch"
                os "LINUX"
                prefix "/"
                summary "RSA Security Analytics"
                vendor "RSA, The Security Division of EMC"
                license "Copyright (c) RSA, The Security Division of EMC"
                user "root"
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.4"
}