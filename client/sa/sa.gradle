ext {
    nginxLocation = "/etc/nginx"
    nginxConfLocation = "${project.nginxLocation}/conf.d"
    nginxHtmlLocation = "/usr/share/nginx/html"

    createRPM = true
    rpmName = "${rootProject.name}-client"
    htmlRootLocation = "/opt/rsa/${rootProject.name}/html"
}

task emberFixturedBuild(type: NodeTask, dependsOn: ["test"]) {
    script = ember
    args = ["build", "-e", "fixtured", "-o", "dist/fixtured", "--silent"]
}

task fixtured(type: Zip, dependsOn: ["emberFixturedBuild"]) {
    destinationDir file(project.buildDir)
    archiveName "sa-ui-fixtured.zip"

    from("dist/fixtured") {
        include "**/*"
    }
}

task nginxConf(type: Copy) {
    from(fileTree("rpm/nginx"))
    into "build/rpm/nginx"
    expand project.properties
}

afterEvaluate {
    rpm {
        dependsOn("nginxConf")

        packageName project.rpmName
        packageDescription "This RPM contains RSA Security Analytics User Interface."

        requires("nginx")

        postInstall file("${project.rpmScriptsDir}/postInstall.sh")

        from(fileTree("build/rpm/nginx")) {
            fileType CONFIG
            into project.nginxLocation
        }

        from(fileTree("dist/production")) {
            into project.htmlRootLocation
        }
    }
}

test.dependsOn(":client:sa:lib/dashboard:build")
build.dependsOn("fixtured", ":client:sa:lib/dashboard:build")
